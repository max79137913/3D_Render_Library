<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  <xsl:output
             method="xml"
             version="1.0"
             encoding="utf-8"
             indent="yes"
             doctype-system="http://dudka.cz/dtd/scene.dtd"
             />
  <xsl:strip-space elements="*"/>
  
  <!-- Match root element -->
  <xsl:template match="/scene">
    <scene>
      <xsl:comment>Generated by XSLT processor using macro.xsl template</xsl:comment>
      <xsl:apply-templates/>
    </scene>
  </xsl:template>
  
  <!-- Copy scene head 1:1 -->
  <xsl:template match="head">
    <xsl:copy-of select="."/>
  </xsl:template>
  
  <!-- Process body part of input document -->
  <xsl:template match="body">
    <body>
      <xsl:apply-templates/>
    </body>
  </xsl:template>
  
  <!-- Recognize macro definition -->
  <xsl:template match="macro">
    <xsl:comment>Defined macro: <xsl:value-of select="@name"/></xsl:comment>
  </xsl:template>
  
  <!-- Execute macro if requested -->
  <xsl:template match="apply-macro">
    <xsl:comment>Start of macro execution: <xsl:value-of select="@name"/></xsl:comment>
    <xsl:variable name="name" select="@name"/>
    <xsl:for-each select="/scene/body/macro[@name=$name]">
      <xsl:apply-templates/>
    </xsl:for-each>
    <xsl:comment>End of macro execution: <xsl:value-of select="@name"/></xsl:comment>
  </xsl:template>
  
  <!-- Copy useful document content -->
  <xsl:template match="*">
    <xsl:copy>
      <xsl:copy-of select="attribute::*"/>
      <xsl:apply-templates/>
    </xsl:copy>
  </xsl:template>
  
</xsl:stylesheet>